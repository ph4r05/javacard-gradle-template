group 'test-applet'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'fr.bmartel:gradle-javacard:1.5.3'
    }
}

apply plugin: 'javacard'
sourceCompatibility = 1.7

// Common settings, definitions
final def rootPath = rootDir.absolutePath
final def libs = rootPath + '/libs'
final def libsSdk = rootPath + '/libs-sdks'

repositories {
    mavenCentral()
    flatDir {
        dirs libs
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.testng:testng:6.1.1'
}

test {
    useTestNG()
    jvmArgs '-noverify'
}

// JavaCard SDKs and libraries
final def JC212 = libsSdk + '/jc212_kit'
final def JC221 = libsSdk + '/jc221_kit'
final def JC222 = libsSdk + '/jc222_kit'
final def JC303 = libsSdk + '/jc303_kit'
final def JC304 = libsSdk + '/jc304_kit'
final def JC305 = libsSdk + '/jc305u1_kit'

// Which JavaCard SDK to use - select
final def JC_SELECTED = JC305

javacard {

    config {
        jckit JC_SELECTED

        cap {
            packageName 'applet'
            version '0.1'
            aid '01:02:03:04:05:06:07:08:09'
            output 'applet.cap'
            applet {
                className 'applet.MainApplet'
                aid '01:02:03:04:05:06:07:08:09:01:02'
            }

            dependencies {
                remote 'fr.bmartel:gplatform:2.1.1'
            }
        }
    }
}

