group 'test-applet'
version '1.0-SNAPSHOT'

// Buildscript configuration for the javacard-gradle plugin.
// Do not modify this particular block. Dependencies for the project are lower.
buildscript {
    repositories {
        mavenCentral()
        // mavenLocal()
    }
    dependencies {
        classpath 'com.klinec:gradle-javacard:1.6.9'
    }
}

apply plugin: 'javacard'
sourceCompatibility = 1.8

// Common settings, definitions
final def rootPath = rootDir.absolutePath
final def libs = rootPath + '/libs'
final def libsSdk = rootPath + '/libs-sdks'

// Repositories for your project
repositories {
    mavenCentral()
    // mavenLocal() // for local maven repository if needed

    // Repository with JCardSim, Globalplatform, etc, ...
    maven {
        url  "https://dl.bintray.com/ph4r05/jcard"
    }

    // Local lib repository
    flatDir {
        dirs libs
    }
}

// Dependencies for your project
dependencies {
    testCompile 'org.testng:testng:6.1.1'
    testCompile 'org.slf4j:slf4j-api:1.7.2'
    testCompile 'org.slf4j:slf4j-log4j12:1.7.2'
    testCompile 'org.apache.logging.log4j:log4j-core:2.11.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.1'
    
    testCompile group: 'com.klinec', name: 'javacard-tools', version: '0.0.2', transitive: false

    jcardsim 'com.klinec:jcardsim:3.0.5.9'
    
    // Include plugin as it has bundled GP & other tools.
    // Alternative: include GP manually, but the included
    // version has to be compatible with the plugin.
    runtimeOnly 'com.klinec:gradle-javacard:1.6.8'
}

test {
    // useTestNG()
    jvmArgs '-noverify'

    useJUnitPlatform {
        excludeTags 'manual'
    }

    task manualTests(type: Test) {
        jvmArgs '-noverify'
        useJUnitPlatform {
            includeTags 'manual'
        }
        check.dependsOn it
        shouldRunAfter test
    }
}

// JavaCard SDKs and libraries
final def JC212 = libsSdk + '/jc212_kit'
final def JC221 = libsSdk + '/jc221_kit'
final def JC222 = libsSdk + '/jc222_kit'
final def JC303 = libsSdk + '/jc303_kit'
final def JC304 = libsSdk + '/jc304_kit'
final def JC305 = libsSdk + '/jc305u1_kit'
final def JC305u2 = libsSdk + '/jc305u2_kit'
final def JC305u3 = libsSdk + '/jc305u3_kit'
final def JC310b43 = libsSdk + '/jc310b43_kit'

// Which JavaCard SDK to use - select
// In order to compile JC222 and lower you have to have Java SDK <= 1.8
final def JC_SELECTED = JC303

javacard {

    //noinspection GroovyAssignabilityCheck
    config {
        jckit JC_SELECTED

        // Using custom repo with jcardsim
        debugGpPro true
        addSurrogateJcardSimRepo false
        addImplicitJcardSim false
        addImplicitJcardSimJunit false

        //noinspection GroovyAssignabilityCheck
        cap {
            packageName 'applet'
            version '0.1'
            aid '01:02:03:04:05:06:07:08:09'
            output 'applet.cap'
            // javaversion '1.7'

            //noinspection GroovyAssignabilityCheck
            applet {
                className 'applet.MainApplet'
                aid '01:02:03:04:05:06:07:08:09:01:02'
            }

            //noinspection GroovyAssignabilityCheck
            // dependencies {
            //     remote 'com.klinec:globalplatform:2.1.1'
            // }
        }
    }
}

